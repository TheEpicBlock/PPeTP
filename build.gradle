plugins {
	id 'fabric-loom' version '1.14-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
}

loom {
	mods {
		"proper-pet-tp" {
			sourceSet sourceSets.main
		}
	}
}

fabricApi {
    configureTests {
        createSourceSet = true
        modId = "ppetp-test"
        enableGameTests = true
        enableClientGameTests = false
        eula = true
    }
}

test {
	useJUnitPlatform()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
	testImplementation "org.junit.jupiter:junit-jupiter:5.10.3"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

sourceSets {
    gametest {
        runtimeClasspath += test.output
        compileClasspath += test.output
    }
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

modrinth {
	token = "$System.env.MODRINTH_TOKEN"
	projectId = project.modrinth_project
	versionNumber = project.version
	uploadFile = remapJar
	loaders = ["fabric", "quilt"]
	changelog = System.getenv("CHANGELOG")
	syncBodyFrom = "<!--DO NOT EDIT MANUALLY: synced from gh readme-->\n" + rootProject.file("README.md").text
	dependencies {
		required.project("fabric-api")
	}
}
